{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  proceso juntarPapeles
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  proceso vaciar
  comenzar
    mientras HayPapelEnLaBolsa
      depositarPapel
    mientras HayFlorEnLaBolsa
      depositarFlor   
  fin
  proceso hacer(E t: numero; E av: numero; E ca: numero)
  variables
    caO,avO:numero
  comenzar
    caO:= PosCa
    avO:= PosAv
    BloquearEsquina(av, ca)
    Pos(av, ca)
    si (t = 1 )
      juntarFlores
    sino
      si(t = 2)
        juntarPapeles
      sino
        vaciar
    Pos(caO,avO)
    LiberarEsquina(av, ca)
  fin
areas
  ciudad: AreaC (1,1,100,100)
robots
  robot slave
  variables
    t, av, ca: numero
  comenzar
    RecibirMensaje(t, jefe)
    mientras (t <> 4)
      RecibirMensaje(av, jefe)
      RecibirMensaje(ca, jefe)
      hacer(t, av, ca)
      EnviarMensaje(V, jefe)
      RecibirMensaje(t, jefe)
  fin
  robot master
  variables
    rr,av, ca, tarea: numero
    ok: boolean
  comenzar
    repetir 10
      Informar(1)
      Random(tarea, 1, 3)
      Random(rr, 1, 2)
      Random(av, 2,100)
      Random(ca, 2, 100)
      si (rr= 1)
        EnviarMensaje(tarea, r1)
        EnviarMensaje(av, r1)
        EnviarMensaje(ca, r1)
        RecibirMensaje(ok, r1)
      sino
        EnviarMensaje(tarea, r2)
        EnviarMensaje(av, r2)
        EnviarMensaje(ca, r2)
        RecibirMensaje(ok, r2)
    EnviarMensaje(4, r1)
    EnviarMensaje(4, r2)
  fin
variables
  r1,r2: slave
  jefe: master
comenzar
  AsignarArea(r1, ciudad)
  AsignarArea(r2, ciudad)
  AsignarArea(jefe, ciudad)
  Iniciar(jefe, 1,1)
  Iniciar(r2, 3,1)
  Iniciar(r1, 2,1)
fin